C  ITRAN.F - TRANSPORT EVALUATION FOR DIAGONAL TENSOR PERMEABILITY
C  BLACK OIL IMPLICIT MODEL

C  ROUTINES IN THIS MODULE:

C  SUBROUTINE ITRANX (IDIM,JDIM,KDIM,LDIM,IL1,IL2,JL1V,JL2V,KL1,KL2,
C                     KEYOUT,NBLK,TCOFX,DEPTH,POIL,PWAT,PGAS,OIL,
C                     DUNK,TRNDAT,COF,RESID,DGRO,TLAMB,SGAS)
C  SUBROUTINE ITRANY (IDIM,JDIM,KDIM,LDIM,IL1,IL2,JL1V,JL2V,KL1,KL2,
C                     KEYOUT,NBLK,TCOFY,DEPTH,POIL,PWAT,PGAS,OIL,
C                     DUNK,TRNDAT,COF,RESID,DGRO,TLAMB,SGAS)
C  SUBROUTINE ITRANZ (IDIM,JDIM,KDIM,LDIM,IL1,IL2,JL1V,JL2V,KL1,KL2,
C                     KEYOUT,NBLK,TCOFZ,DEPTH,POIL,PWAT,PGAS,OIL,
C                     DUNK,TRNDAT,COF,RESID,DGRO,TLAMB,SGAS)

C  CODE HISTORY:

C  JOHN WHEELER     8/01/97    INITIAL VERSION

C  QIN LU           9/04/97   ADDED GAS PHASE AND Y,Z DIRECTIONS

C  QIN LU           9/18/97   CHANGE INDEPENDENT VARIABLES (No,Pw,Ng) TO
C                             (Pw,No,Ng) FOR THE SOLVER

C  QIN LU           9/03/98   DON'T NEED TO SEPARATE 2-PHASE AND 3-PHASE IN
C                             FORMULAS ANY MORE

C  JOHN WHEELER    10/10/99   REDUCED STORAGE REQUIREMENT AND SWITCHED FROM
C                             Pw TO Po PRIMARY VARIABLE

C  JOHN WHEELER     5/21/01   REMOVE JACOBIAN APPROXIMATION Pw = Po AND Pg = Po
C                             IMPROVE GAS PHASE FLOW TO AN ELEMENT THAT
C                             CONTAINS NO GAS PHASE
C*********************************************************************
      SUBROUTINE ITRANX (IDIM,JDIM,KDIM,LDIM,IL1,IL2,JL1V,JL2V,KL1,KL2,
     &                   KEYOUT,NBLK,TCOFX,DEPTH,POIL,PWAT,PGAS,OIL,
     &                   DUNK,TRNDAT,COF,RESID,DGRO,TLAMB,SGAS)
C*********************************************************************

C  Black oil model x transport for diagonal tensor permeability

C  TCOFX(I,J,K) = X TRANSMISSABILITY CONSTANT, CU-FT CP / PSI DAY
C                 (INPUT, REAL*8)

C  DEPTH(I,J,K) = ELEMENT CENTER DEPTH, FT (INPUT, REAL*8)

C  POIL(I,J,K) = OIL PRESSURE, PSI (INPUT, REAL*8)

C  PWAT(I,J,K) = WATER PRESSURE, PSI (INPUT, REAL*8)

C  PGAS(I,J,K) = GAS PRESSURE, PSI (INPUT, REAL*8)

C  OIL(I,J,K) = OIL STOCK TANK VOLUME / UNIT PORE VOLUME (INPUT, REAL*8)

C  DUNK(I,J,K,1) = OIL DENSITY, LB/CU-FT (INPUT, REAL*8)
C  DUNK(I,J,K,2) = WATER DENSITY, LB/CU-FT (INPUT, REAL*8)
C  DUNK(I,J,K,3) = GAS DENSITY, LB/CU-FT (INPUT, REAL*8)

C  TRNDAT(I,J,K,) = MISC. PHYSICAL DATA (INPUT, REAL*4)
C                   (SEE IARYDAT.H FOR DEFINITIONS)

C  COF(I,J,K,N,L,M) = JACOBIAN COEFFICIENTS (INPUT AND OUTPUT, REAL*4)

C  RESID(I,J,K,N) = RESIDUALS (INPUT AND OUTPUT, REAL*8)

C  DGRO(I,J,K) = DISOLVED GAS-OIL RATIO (INPUT, REAL*8)

C  TLAMB(I,J,K,n) = LAMBDA OF PHASE n (INPUT, REAL*8)

C  SGAS(I,J,K) = GAS SATURATION (INPUT, REAL*4)

C*********************************************************************
      INCLUDE 'control.h'
      INCLUDE 'layout.h'

      INCLUDE 'ifluid.h'

      INTEGER JL1V(KDIM),JL2V(KDIM),      KEYOUT(IDIM,JDIM,KDIM)
      REAL*8 TCOFX(IDIM,JDIM,KDIM),       DEPTH(IDIM,JDIM,KDIM),
     &       POIL(IDIM,JDIM,KDIM),        PWAT(IDIM,JDIM,KDIM),
     &       PGAS(IDIM,JDIM,KDIM),        DUNK(IDIM,JDIM,KDIM,3),
     &       RESID(IDIM,JDIM,KDIM,3),     DGRO(IDIM,JDIM,KDIM),
     &       TLAMB(IDIM,JDIM,KDIM,3),     OIL(IDIM,JDIM,KDIM)
      REAL*4 TRNDAT(IDIM,JDIM,KDIM,*),    SGAS(IDIM,JDIM,KDIM)
      REAL*4 COF(IDIM,JDIM,KDIM,$NUMCOF,3,3)
      REAL*8 DP,RO,FLUX,DD,G2,TC,DZ,DM,PL,TVW,TVO,TVG,TCP
      LOGICAL NOGASI,NOGASM

C  SET GRAVITY CONSTANT TO .5 g WITH UNITS PSI BBL / FT LB
C  CONVERT TRANSMISSABILITY CONSTANT TO BBL CP / PSI DAY

      G2=.08905379276541D0*GRAV
      TVW=.178107585530824D0*DELTIM/WATVIS
      TVO=.178107585530824D0*DELTIM/OILVIS
      TVG=.178107585530824D0*DELTIM/GASVIS

C  LOOP OVER ELEMENT FACES AT I - 1/2

      IL2P=IL2+1
      DO 1 K=KL1,KL2
      JL1=JL1V(K)
      JL2=JL2V(K)
      DO 1 J=JL1,JL2
      DO 1 I=IL1,IL2P

      TC=TCOFX(I,J,K)
      IF (TC.NE.0.D0) THEN

         DD=(DEPTH(I,J,K)-DEPTH(I-1,J,K))*G2
         DDS=DD
         IF (SGAS(I,J,K).EQ.0.) THEN
            NOGASI=.TRUE.
         ELSE
            NOGASI=.FALSE.
         ENDIF
         IF (SGAS(I-1,J,K).EQ.0.) THEN
            NOGASM=.TRUE.
         ELSE
            NOGASM=.FALSE.
         ENDIF

C  OIL AND DISOLVED GAS, X TRANSPORT AT I - 1/2

         TCP=TC*TVO
         DP=POIL(I,J,K)-POIL(I-1,J,K)-DD*(DUNK(I,J,K,1)+DUNK(I-1,J,K,1))
         DUM=TCP*DP

         IF (DP.LT.0.D0) THEN
            PL=TLAMB(I-1,J,K,1)
            ALAM=TCP*PL
            FLUX=TCP*PL*DP
            RO=DGRO(I-1,J,K)
            ROS=RO
            C11=ALAM*(DDS*TRNDAT(I,J,K,11)-1.)
            C11G=ROS*C11
            IF (NOGASI) THEN
               C12=ALAM*DDS*TRNDAT(I,J,K,8)
               C12G=ROS*C12
               C13=ALAM*DDS*TRNDAT(I,J,K,9)
               C13G=ROS*C13
            ELSE
               C12=0.
               C12G=0.
               C13=0.
               C13G=0.
            ENDIF
            C21=ALAM*(DDS*TRNDAT(I-1,J,K,11)+1.)-DUM*TRNDAT(I-1,J,K,2)
            IF (NOGASM) THEN
               C22=ALAM*DDS*TRNDAT(I-1,J,K,8)-DUM*TRNDAT(I-1,J,K,1)
               C23=ALAM*DDS*TRNDAT(I-1,J,K,9)-DUM*TRNDAT(I-1,J,K,3)
               C21G=ROS*C21
               DUM=FLUX/OIL(I-1,J,K)
               C22G=ROS*(C22+DUM)
               C23G=ROS*C23-DUM
            ELSE
               C22=-DUM*TRNDAT(I-1,J,K,1)
               C23=-DUM*TRNDAT(I-1,J,K,3)
               C21G=ROS*C21-FLUX*TRNDAT(I-1,J,K,13)
               C22G=ROS*C22
               C23G=ROS*C23
            ENDIF

         ELSE

            PL=TLAMB(I,J,K,1)
            FLUX=TCP*PL*DP
            ALAM=TCP*PL
            RO=DGRO(I,J,K)
            ROS=RO
            C11=ALAM*(DDS*TRNDAT(I,J,K,11)-1.)-DUM*TRNDAT(I,J,K,2)
            C21=ALAM*(DDS*TRNDAT(I-1,J,K,11)+1.)
            C21G=ROS*C21
            IF (NOGASM) THEN
               C22=ALAM*DDS*TRNDAT(I-1,J,K,8)
               C22G=ROS*C22
               C23=ALAM*DDS*TRNDAT(I-1,J,K,9)
               C23G=ROS*C23
            ELSE
               C22=0.
               C23=0.
               C22G=0.
               C23G=0.
            ENDIF
            IF (NOGASI) THEN
               C12=ALAM*DDS*TRNDAT(I,J,K,8)-DUM*TRNDAT(I,J,K,1)
               C13=ALAM*DDS*TRNDAT(I,J,K,9)-DUM*TRNDAT(I,J,K,3)
               C11G=ROS*C11
               DUM=FLUX/OIL(I,J,K)
               C12G=ROS*(C12+DUM)
               C13G=ROS*C13-DUM
            ELSE
               C12=-DUM*TRNDAT(I,J,K,1)
               C13=-DUM*TRNDAT(I,J,K,3)
               C11G=ROS*C11-FLUX*TRNDAT(I,J,K,13)
               C12G=ROS*C12
               C13G=ROS*C13
            ENDIF

         ENDIF

         IF (KEYOUT(I,J,K).GT.0) THEN
            COF(I,J,K,1,2,1)=COF(I,J,K,1,2,1)+C11
            COF(I,J,K,1,2,2)=COF(I,J,K,1,2,2)+C12
            COF(I,J,K,1,2,3)=COF(I,J,K,1,2,3)+C13
            COF(I,J,K,2,2,1)=C21
            COF(I,J,K,2,2,2)=C22
            COF(I,J,K,2,2,3)=C23
            RESID(I,J,K,2)=RESID(I,J,K,2)+FLUX
         ENDIF
         IF (KEYOUT(I-1,J,K).GT.0) THEN
            COF(I-1,J,K,3,2,1)=-C11
            COF(I-1,J,K,3,2,2)=-C12
            COF(I-1,J,K,3,2,3)=-C13
            COF(I-1,J,K,1,2,1)=COF(I-1,J,K,1,2,1)-C21
            COF(I-1,J,K,1,2,2)=COF(I-1,J,K,1,2,2)-C22
            COF(I-1,J,K,1,2,3)=COF(I-1,J,K,1,2,3)-C23
            RESID(I-1,J,K,2)=RESID(I-1,J,K,2)-FLUX
         ENDIF

C  FREE GAS, X TRANSPORT AT I - 1/2

         FLUX=RO*FLUX
         IF (NOGASI.AND.NOGASM) GO TO 2

         TCP=TC*TVG
         IF (NOGASI) THEN
            DZ=DUNK(I-1,J,K,3)
         ELSE
            DZ=DUNK(I,J,K,3)
         ENDIF
         IF (NOGASM) THEN
            DM=DZ
         ELSE
            DM=DUNK(I-1,J,K,3)
         ENDIF
         DP=PGAS(I,J,K)-PGAS(I-1,J,K)-DD*(DZ+DM)
         DUM=TCP*DP
         IF (DP.LT.0.D0) THEN
            IF (NOGASM) GO TO 2
            PL=TLAMB(I-1,J,K,3)
            ALAM=TCP*PL
            IF (NOGASI) THEN
               C11G=C11G-ALAM
               DUM1=ALAM*(2.*DDS*TRNDAT(I-1,J,K,12)+1.)
               C21G=C21G+DUM1*TRNDAT(I-1,J,K,17)-DUM*TRNDAT(I-1,J,K,7)
               C22G=C22G+DUM1*TRNDAT(I-1,J,K,18)-DUM*TRNDAT(I-1,J,K,8)
               C23G=C23G+DUM1*TRNDAT(I-1,J,K,19)-DUM*TRNDAT(I-1,J,K,9)
            ELSE
               DUM1=ALAM*(DDS*TRNDAT(I,J,K,12)-1.)
               C11G=C11G+DUM1*TRNDAT(I,J,K,17)
               C12G=C12G+DUM1*TRNDAT(I,J,K,18)
               C13G=C13G+DUM1*TRNDAT(I,J,K,19)
               DUM1=ALAM*(DDS*TRNDAT(I-1,J,K,12)+1.)
               C21G=C21G+DUM1*TRNDAT(I-1,J,K,17)-DUM*TRNDAT(I-1,J,K,7)
               C22G=C22G+DUM1*TRNDAT(I-1,J,K,18)-DUM*TRNDAT(I-1,J,K,8)
               C23G=C23G+DUM1*TRNDAT(I-1,J,K,19)-DUM*TRNDAT(I-1,J,K,9)
            ENDIF
         ELSE
            IF (NOGASI) GO TO 2
            PL=TLAMB(I,J,K,3)
            ALAM=TCP*PL
            IF (NOGASM) THEN
               DUM1=ALAM*(2.*DDS*TRNDAT(I,J,K,12)-1.)
               C11G=C11G+DUM1*TRNDAT(I,J,K,17)-DUM*TRNDAT(I,J,K,7)
               C12G=C12G+DUM1*TRNDAT(I,J,K,18)-DUM*TRNDAT(I,J,K,8)
               C13G=C13G+DUM1*TRNDAT(I,J,K,19)-DUM*TRNDAT(I,J,K,9)
               C21G=C21G+ALAM
            ELSE
               DUM1=ALAM*(DDS*TRNDAT(I,J,K,12)-1.)
               C11G=C11G+DUM1*TRNDAT(I,J,K,17)-DUM*TRNDAT(I,J,K,7)
               C12G=C12G+DUM1*TRNDAT(I,J,K,18)-DUM*TRNDAT(I,J,K,8)
               C13G=C13G+DUM1*TRNDAT(I,J,K,19)-DUM*TRNDAT(I,J,K,9)
               DUM1=ALAM*(DDS*TRNDAT(I-1,J,K,12)+1.)
               C21G=C21G+DUM1*TRNDAT(I-1,J,K,17)
               C22G=C22G+DUM1*TRNDAT(I-1,J,K,18)
               C23G=C23G+DUM1*TRNDAT(I-1,J,K,19)
            ENDIF
         ENDIF
         FLUX=FLUX+TCP*PL*DP

    2    IF (KEYOUT(I,J,K).GT.0) THEN
            COF(I,J,K,1,3,1)=COF(I,J,K,1,3,1)+C11G
            COF(I,J,K,1,3,2)=COF(I,J,K,1,3,2)+C12G
            COF(I,J,K,1,3,3)=COF(I,J,K,1,3,3)+C13G
            COF(I,J,K,2,3,1)=C21G
            COF(I,J,K,2,3,2)=C22G
            COF(I,J,K,2,3,3)=C23G
            RESID(I,J,K,3)=RESID(I,J,K,3)+FLUX
         ENDIF
         IF (KEYOUT(I-1,J,K).GT.0) THEN
            COF(I-1,J,K,3,3,1)=-C11G
            COF(I-1,J,K,3,3,2)=-C12G
            COF(I-1,J,K,3,3,3)=-C13G
            COF(I-1,J,K,1,3,1)=COF(I-1,J,K,1,3,1)-C21G
            COF(I-1,J,K,1,3,2)=COF(I-1,J,K,1,3,2)-C22G
            COF(I-1,J,K,1,3,3)=COF(I-1,J,K,1,3,3)-C23G
            RESID(I-1,J,K,3)=RESID(I-1,J,K,3)-FLUX
         ENDIF

C  WATER EQUATION, X TRANSPORT AT I - 1/2

         TCP=TC*TVW
         DP=PWAT(I,J,K)-PWAT(I-1,J,K)-DD*(DUNK(I,J,K,2)+DUNK(I-1,J,K,2))
         DUM=TCP*DP
         IF (DP.LT.0.D0) THEN
            PL=TLAMB(I-1,J,K,2)
            ALAM=TCP*PL
            DUM1=ALAM*(DDS*TRNDAT(I,J,K,10)-1.)
            C11=DUM1*TRNDAT(I,J,K,14)
            C12=DUM1*TRNDAT(I,J,K,15)
            C13=DUM1*TRNDAT(I,J,K,16)
            DUM1=ALAM*(DDS*TRNDAT(I-1,J,K,10)+1.)
            C21=DUM1*TRNDAT(I-1,J,K,14)-DUM*TRNDAT(I-1,J,K,5)
            C22=DUM1*TRNDAT(I-1,J,K,15)-DUM*TRNDAT(I-1,J,K,4)
            C23=DUM1*TRNDAT(I-1,J,K,16)-DUM*TRNDAT(I-1,J,K,6)
         ELSE
            PL=TLAMB(I,J,K,2)
            ALAM=TCP*PL
            DUM1=ALAM*(DDS*TRNDAT(I,J,K,10)-1.)
            C11=DUM1*TRNDAT(I,J,K,14)-DUM*TRNDAT(I,J,K,5)
            C12=DUM1*TRNDAT(I,J,K,15)-DUM*TRNDAT(I,J,K,4)
            C13=DUM1*TRNDAT(I,J,K,16)-DUM*TRNDAT(I,J,K,6)
            DUM1=ALAM*(DDS*TRNDAT(I-1,J,K,10)+1.)
            C21=DUM1*TRNDAT(I-1,J,K,14)
            C22=DUM1*TRNDAT(I-1,J,K,15)
            C23=DUM1*TRNDAT(I-1,J,K,16)
         ENDIF
         FLUX=TCP*PL*DP

         IF (KEYOUT(I,J,K).GT.0) THEN
            COF(I,J,K,1,1,1)=COF(I,J,K,1,1,1)+C11
            COF(I,J,K,1,1,2)=COF(I,J,K,1,1,2)+C12
            COF(I,J,K,1,1,3)=COF(I,J,K,1,1,3)+C13
            COF(I,J,K,2,1,1)=C21
            COF(I,J,K,2,1,2)=C22
            COF(I,J,K,2,1,3)=C23
            RESID(I,J,K,1)=RESID(I,J,K,1)+FLUX
         ENDIF
         IF (KEYOUT(I-1,J,K).GT.0) THEN
            COF(I-1,J,K,3,1,1)=-C11
            COF(I-1,J,K,3,1,2)=-C12
            COF(I-1,J,K,3,1,3)=-C13
            COF(I-1,J,K,1,1,1)=COF(I-1,J,K,1,1,1)-C21
            COF(I-1,J,K,1,1,2)=COF(I-1,J,K,1,1,2)-C22
            COF(I-1,J,K,1,1,3)=COF(I-1,J,K,1,1,3)-C23
            RESID(I-1,J,K,1)=RESID(I-1,J,K,1)-FLUX
         ENDIF

C  NO X TRANSPORT AT I - 1/2

      ELSE

         IF (KEYOUT(I,J,K).EQ.0) THEN
            COF(I,J,K,2,1,1)=0.
            COF(I,J,K,2,1,2)=0.
            COF(I,J,K,2,1,3)=0.
            COF(I,J,K,2,2,1)=0.
            COF(I,J,K,2,2,2)=0.
            COF(I,J,K,2,2,3)=0.
            COF(I,J,K,2,3,1)=0.
            COF(I,J,K,2,3,2)=0.
            COF(I,J,K,2,3,3)=0.
         ENDIF
         IF (I.GT.1.AND.KEYOUT(I-1,J,K).EQ.0) THEN
            COF(I-1,J,K,3,1,1)=0.
            COF(I-1,J,K,3,1,2)=0.
            COF(I-1,J,K,3,1,3)=0.
            COF(I-1,J,K,3,2,1)=0.
            COF(I-1,J,K,3,2,2)=0.
            COF(I-1,J,K,3,2,3)=0.
            COF(I-1,J,K,3,3,1)=0.
            COF(I-1,J,K,3,3,2)=0.
            COF(I-1,J,K,3,3,3)=0.
         ENDIF

      ENDIF

    1 CONTINUE
      END

C*********************************************************************
      SUBROUTINE ITRANY (IDIM,JDIM,KDIM,LDIM,IL1,IL2,JL1V,JL2V,KL1,KL2,
     &                   KEYOUT,NBLK,TCOFY,DEPTH,POIL,PWAT,PGAS,OIL,
     &                   DUNK,TRNDAT,COF,RESID,DGRO,TLAMB,SGAS)
C*********************************************************************

C  Black oil model y transport for diagonal tensor permeability

C  TCOFY(I,J,K) = Y TRANSMISSABILITY CONSTANT, CU-FT CP / PSI DAY
C                 (INPUT, REAL*8)

C  DEPTH(I,J,K) = ELEMENT CENTER DEPTH, FT (INPUT, REAL*8)

C  POIL(I,J,K) = OIL PRESSURE, PSI (INPUT, REAL*8)

C  PWAT(I,J,K) = WATER PRESSURE, PSI (INPUT, REAL*8)

C  PGAS(I,J,K) = GAS PRESSURE, PSI (INPUT, REAL*8)

C  OIL(I,J,K) = OIL STOCK TANK VOLUME / UNIT PORE VOLUME (INPUT, REAL*8)

C  DUNK(I,J,K,1) = OIL DENSITY, LB/CU-FT (INPUT, REAL*8)
C  DUNK(I,J,K,2) = WATER DENSITY, LB/CU-FT (INPUT, REAL*8)
C  DUNK(I,J,K,3) = GAS DENSITY, LB/CU-FT (INPUT, REAL*8)

C  TRNDAT(I,J,K,) = MISC. PHYSICAL DATA (INPUT, REAL*4)
C                   (SEE IARYDAT.H FOR DEFINITIONS)

C  COF(I,J,K,N,L,M) = JACOBIAN COEFFICIENTS (INPUT AND OUTPUT, REAL*4)

C  RESID(I,J,K,N) = RESIDUALS (INPUT AND OUTPUT, REAL*8)

C  DGRO(I,J,K) = DISOLVED GAS-OIL RATIO (INPUT, REAL*8)

C  TLAMB(I,J,K,n) = LAMBDA OF PHASE n (INPUT, REAL*8)

C  SGAS(I,J,K) = GAS SATURATION (INPUT, REAL*4)

C*********************************************************************
      INCLUDE 'control.h'
      INCLUDE 'layout.h'

      INCLUDE 'ifluid.h'

      INTEGER JL1V(KDIM),JL2V(KDIM),      KEYOUT(IDIM,JDIM,KDIM)
      REAL*8 TCOFY(IDIM,JDIM,KDIM),       DEPTH(IDIM,JDIM,KDIM),
     &       POIL(IDIM,JDIM,KDIM),        PWAT(IDIM,JDIM,KDIM),
     &       PGAS(IDIM,JDIM,KDIM),        DUNK(IDIM,JDIM,KDIM,3),
     &       RESID(IDIM,JDIM,KDIM,3),     DGRO(IDIM,JDIM,KDIM),
     &       TLAMB(IDIM,JDIM,KDIM,3),     OIL(IDIM,JDIM,KDIM)
      REAL*4 TRNDAT(IDIM,JDIM,KDIM,*),    SGAS(IDIM,JDIM,KDIM)
      REAL*4 COF(IDIM,JDIM,KDIM,$NUMCOF,3,3)
      REAL*8 DP,RO,FLUX,DD,G2,TC,DZ,DM,PL,TVW,TVO,TVG,TCP
      LOGICAL NOGASI,NOGASM

C  SET GRAVITY CONSTANT TO .5 g WITH UNITS PSI BBL / FT LB

      G2=.08905379276541D0*GRAV
      TVW=.178107585530824D0*DELTIM/WATVIS
      TVO=.178107585530824D0*DELTIM/OILVIS
      TVG=.178107585530824D0*DELTIM/GASVIS

C  LOOP OVER ELEMENT FACES AT J - 1/2

      DO 1 K=KL1,KL2
      JL1=JL1V(K)
      JL2P=JL2V(K)+1
      DO 1 J=JL1,JL2P
      DO 1 I=IL1,IL2

      TC=TCOFY(I,J,K)
      IF (TC.NE.0.D0) THEN

         DD=(DEPTH(I,J,K)-DEPTH(I,J-1,K))*G2
         DDS=DD
         IF (SGAS(I,J,K).EQ.0.) THEN
            NOGASI=.TRUE.
         ELSE
            NOGASI=.FALSE.
         ENDIF
         IF (SGAS(I,J-1,K).EQ.0.) THEN
            NOGASM=.TRUE.
         ELSE
            NOGASM=.FALSE.
         ENDIF

C  OIL AND DISOLVED GAS, Y TRANSPORT AT J - 1/2

         TCP=TC*TVO
         DP=POIL(I,J,K)-POIL(I,J-1,K)-DD*(DUNK(I,J,K,1)+DUNK(I,J-1,K,1))
         DUM=TCP*DP

         IF (DP.LT.0.D0) THEN
            PL=TLAMB(I,J-1,K,1)
            ALAM=TCP*PL
            FLUX=TCP*PL*DP
            RO=DGRO(I,J-1,K)
            ROS=RO
            C11=ALAM*(DDS*TRNDAT(I,J,K,11)-1.)
            C11G=ROS*C11
            IF (NOGASI) THEN
               C12=ALAM*DDS*TRNDAT(I,J,K,8)
               C12G=ROS*C12
               C13=ALAM*DDS*TRNDAT(I,J,K,9)
               C13G=ROS*C13
            ELSE
               C12=0.
               C12G=0.
               C13=0.
               C13G=0.
            ENDIF
            C21=ALAM*(DDS*TRNDAT(I,J-1,K,11)+1.)-DUM*TRNDAT(I,J-1,K,2)
            IF (NOGASM) THEN
               C22=ALAM*DDS*TRNDAT(I,J-1,K,8)-DUM*TRNDAT(I,J-1,K,1)
               C23=ALAM*DDS*TRNDAT(I,J-1,K,9)-DUM*TRNDAT(I,J-1,K,3)
               C21G=ROS*C21
               DUM=FLUX/OIL(I,J-1,K)
               C22G=ROS*(C22+DUM)
               C23G=ROS*C23-DUM
            ELSE
               C22=-DUM*TRNDAT(I,J-1,K,1)
               C23=-DUM*TRNDAT(I,J-1,K,3)
               C21G=ROS*C21-FLUX*TRNDAT(I,J-1,K,13)
               C22G=ROS*C22
               C23G=ROS*C23
            ENDIF

         ELSE

            PL=TLAMB(I,J,K,1)
            FLUX=TCP*PL*DP
            ALAM=TCP*PL
            RO=DGRO(I,J,K)
            ROS=RO
            C11=ALAM*(DDS*TRNDAT(I,J,K,11)-1.)-DUM*TRNDAT(I,J,K,2)
            C21=ALAM*(DDS*TRNDAT(I,J-1,K,11)+1.)
            C21G=ROS*C21
            IF (NOGASM) THEN
               C22=ALAM*DDS*TRNDAT(I,J-1,K,8)
               C22G=ROS*C22
               C23=ALAM*DDS*TRNDAT(I,J-1,K,9)
               C23G=ROS*C23
            ELSE
               C22=0.
               C23=0.
               C22G=0.
               C23G=0.
            ENDIF
            IF (NOGASI) THEN
               C12=ALAM*DDS*TRNDAT(I,J,K,8)-DUM*TRNDAT(I,J,K,1)
               C13=ALAM*DDS*TRNDAT(I,J,K,9)-DUM*TRNDAT(I,J,K,3)
               C11G=ROS*C11
               DUM=FLUX/OIL(I,J,K)
               C12G=ROS*(C12+DUM)
               C13G=ROS*C13-DUM
            ELSE
               C12=-DUM*TRNDAT(I,J,K,1)
               C13=-DUM*TRNDAT(I,J,K,3)
               C11G=ROS*C11-FLUX*TRNDAT(I,J,K,13)
               C12G=ROS*C12
               C13G=ROS*C13
            ENDIF

         ENDIF

         IF (KEYOUT(I,J,K).GT.0) THEN
            COF(I,J,K,1,2,1)=COF(I,J,K,1,2,1)+C11
            COF(I,J,K,1,2,2)=COF(I,J,K,1,2,2)+C12
            COF(I,J,K,1,2,3)=COF(I,J,K,1,2,3)+C13
            COF(I,J,K,4,2,1)=C21
            COF(I,J,K,4,2,2)=C22
            COF(I,J,K,4,2,3)=C23
            RESID(I,J,K,2)=RESID(I,J,K,2)+FLUX
         ENDIF
         IF (KEYOUT(I,J-1,K).GT.0) THEN
            COF(I,J-1,K,5,2,1)=-C11
            COF(I,J-1,K,5,2,2)=-C12
            COF(I,J-1,K,5,2,3)=-C13
            COF(I,J-1,K,1,2,1)=COF(I,J-1,K,1,2,1)-C21
            COF(I,J-1,K,1,2,2)=COF(I,J-1,K,1,2,2)-C22
            COF(I,J-1,K,1,2,3)=COF(I,J-1,K,1,2,3)-C23
            RESID(I,J-1,K,2)=RESID(I,J-1,K,2)-FLUX
         ENDIF

C FREE GAS, Y TRANSPORT AT J - 1/2

         FLUX=RO*FLUX
         IF (NOGASI.AND.NOGASM) GO TO 2

         TCP=TC*TVG
         IF (NOGASI) THEN
            DZ=DUNK(I,J-1,K,3)
         ELSE
            DZ=DUNK(I,J,K,3)
         ENDIF
         IF (NOGASM) THEN
            DM=DZ
         ELSE
            DM=DUNK(I,J-1,K,3)
         ENDIF
         DP=PGAS(I,J,K)-PGAS(I,J-1,K)-DD*(DZ+DM)
         DUM=TCP*DP
         IF (DP.LT.0.D0) THEN
            IF (NOGASM) GO TO 2
            PL=TLAMB(I,J-1,K,3)
            ALAM=TCP*PL
            IF (NOGASI) THEN
               C11G=C11G-ALAM
               DUM1=ALAM*(2.*DDS*TRNDAT(I,J-1,K,12)+1.)
               C21G=C21G+DUM1*TRNDAT(I,J-1,K,17)-DUM*TRNDAT(I,J-1,K,7)
               C22G=C22G+DUM1*TRNDAT(I,J-1,K,18)-DUM*TRNDAT(I,J-1,K,8)
               C23G=C23G+DUM1*TRNDAT(I,J-1,K,19)-DUM*TRNDAT(I,J-1,K,9)
            ELSE
               DUM1=ALAM*(DDS*TRNDAT(I,J,K,12)-1.)
               C11G=C11G+DUM1*TRNDAT(I,J,K,17)
               C12G=C12G+DUM1*TRNDAT(I,J,K,18)
               C13G=C13G+DUM1*TRNDAT(I,J,K,19)
               DUM1=ALAM*(DDS*TRNDAT(I,J-1,K,12)+1.)
               C21G=C21G+DUM1*TRNDAT(I,J-1,K,17)-DUM*TRNDAT(I,J-1,K,7)
               C22G=C22G+DUM1*TRNDAT(I,J-1,K,18)-DUM*TRNDAT(I,J-1,K,8)
               C23G=C23G+DUM1*TRNDAT(I,J-1,K,19)-DUM*TRNDAT(I,J-1,K,9)
            ENDIF
         ELSE
            IF (NOGASI) GO TO 2
            PL=TLAMB(I,J,K,3)
            ALAM=TCP*PL
            IF (NOGASM) THEN
               DUM1=ALAM*(2.*DDS*TRNDAT(I,J,K,12)-1.)
               C11G=C11G+DUM1*TRNDAT(I,J,K,17)-DUM*TRNDAT(I,J,K,7)
               C12G=C12G+DUM1*TRNDAT(I,J,K,18)-DUM*TRNDAT(I,J,K,8)
               C13G=C13G+DUM1*TRNDAT(I,J,K,19)-DUM*TRNDAT(I,J,K,9)
               C21G=C21G+ALAM
            ELSE
               DUM1=ALAM*(DDS*TRNDAT(I,J,K,12)-1.)
               C11G=C11G+DUM1*TRNDAT(I,J,K,17)-DUM*TRNDAT(I,J,K,7)
               C12G=C12G+DUM1*TRNDAT(I,J,K,18)-DUM*TRNDAT(I,J,K,8)
               C13G=C13G+DUM1*TRNDAT(I,J,K,19)-DUM*TRNDAT(I,J,K,9)
               DUM1=ALAM*(DDS*TRNDAT(I,J-1,K,12)+1.)
               C21G=C21G+DUM1*TRNDAT(I,J-1,K,17)
               C22G=C22G+DUM1*TRNDAT(I,J-1,K,18)
               C23G=C23G+DUM1*TRNDAT(I,J-1,K,19)
            ENDIF
         ENDIF
         FLUX=FLUX+TCP*PL*DP

    2    IF (KEYOUT(I,J,K).GT.0) THEN
            COF(I,J,K,1,3,1)=COF(I,J,K,1,3,1)+C11G
            COF(I,J,K,1,3,2)=COF(I,J,K,1,3,2)+C12G
            COF(I,J,K,1,3,3)=COF(I,J,K,1,3,3)+C13G
            COF(I,J,K,4,3,1)=C21G
            COF(I,J,K,4,3,2)=C22G
            COF(I,J,K,4,3,3)=C23G
            RESID(I,J,K,3)=RESID(I,J,K,3)+FLUX
         ENDIF
         IF (KEYOUT(I,J-1,K).GT.0) THEN
            COF(I,J-1,K,5,3,1)=-C11G
            COF(I,J-1,K,5,3,2)=-C12G
            COF(I,J-1,K,5,3,3)=-C13G
            COF(I,J-1,K,1,3,1)=COF(I,J-1,K,1,3,1)-C21G
            COF(I,J-1,K,1,3,2)=COF(I,J-1,K,1,3,2)-C22G
            COF(I,J-1,K,1,3,3)=COF(I,J-1,K,1,3,3)-C23G
            RESID(I,J-1,K,3)=RESID(I,J-1,K,3)-FLUX
         ENDIF

C  WATER EQUATION, Y TRANSPORT AT J - 1/2

         TCP=TC*TVW
         DP=PWAT(I,J,K)-PWAT(I,J-1,K)-DD*(DUNK(I,J,K,2)+DUNK(I,J-1,K,2))
         DUM=TCP*DP
         IF (DP.LT.0.D0) THEN
            PL=TLAMB(I,J-1,K,2)
            ALAM=TCP*PL
            DUM1=ALAM*(DDS*TRNDAT(I,J,K,10)-1.)
            C11=DUM1*TRNDAT(I,J,K,14)
            C12=DUM1*TRNDAT(I,J,K,15)
            C13=DUM1*TRNDAT(I,J,K,16)
            DUM1=ALAM*(DDS*TRNDAT(I,J-1,K,10)+1.)
            C21=DUM1*TRNDAT(I,J-1,K,14)-DUM*TRNDAT(I,J-1,K,5)
            C22=DUM1*TRNDAT(I,J-1,K,15)-DUM*TRNDAT(I,J-1,K,4)
            C23=DUM1*TRNDAT(I,J-1,K,16)-DUM*TRNDAT(I,J-1,K,6)
         ELSE
            PL=TLAMB(I,J,K,2)
            ALAM=TCP*PL
            DUM1=ALAM*(DDS*TRNDAT(I,J,K,10)-1.)
            C11=DUM1*TRNDAT(I,J,K,14)-DUM*TRNDAT(I,J,K,5)
            C12=DUM1*TRNDAT(I,J,K,15)-DUM*TRNDAT(I,J,K,4)
            C13=DUM1*TRNDAT(I,J,K,16)-DUM*TRNDAT(I,J,K,6)
            DUM1=ALAM*(DDS*TRNDAT(I,J-1,K,10)+1.)
            C21=DUM1*TRNDAT(I,J-1,K,14)
            C22=DUM1*TRNDAT(I,J-1,K,15)
            C23=DUM1*TRNDAT(I,J-1,K,16)
         ENDIF
         FLUX=TCP*PL*DP

         IF (KEYOUT(I,J,K).GT.0) THEN
            COF(I,J,K,1,1,1)=COF(I,J,K,1,1,1)+C11
            COF(I,J,K,1,1,2)=COF(I,J,K,1,1,2)+C12
            COF(I,J,K,1,1,3)=COF(I,J,K,1,1,3)+C13
            COF(I,J,K,4,1,1)=C21
            COF(I,J,K,4,1,2)=C22
            COF(I,J,K,4,1,3)=C23
            RESID(I,J,K,1)=RESID(I,J,K,1)+FLUX
         ENDIF
         IF (KEYOUT(I,J-1,K).GT.0) THEN
            COF(I,J-1,K,5,1,1)=-C11
            COF(I,J-1,K,5,1,2)=-C12
            COF(I,J-1,K,5,1,3)=-C13
            COF(I,J-1,K,1,1,1)=COF(I,J-1,K,1,1,1)-C21
            COF(I,J-1,K,1,1,2)=COF(I,J-1,K,1,1,2)-C22
            COF(I,J-1,K,1,1,3)=COF(I,J-1,K,1,1,3)-C23
            RESID(I,J-1,K,1)=RESID(I,J-1,K,1)-FLUX
         ENDIF

C  NO Y TRANSPORT AT J - 1/2

      ELSE

         IF (KEYOUT(I,J,K).EQ.0) THEN
            COF(I,J,K,4,1,1)=0.
            COF(I,J,K,4,1,2)=0.
            COF(I,J,K,4,1,3)=0.
            COF(I,J,K,4,2,1)=0.
            COF(I,J,K,4,2,2)=0.
            COF(I,J,K,4,2,3)=0.
            COF(I,J,K,4,3,1)=0.
            COF(I,J,K,4,3,2)=0.
            COF(I,J,K,4,3,3)=0.
         ENDIF
         IF (I.GT.1.AND.KEYOUT(I,J-1,K).EQ.0) THEN
            COF(I,J-1,K,5,1,1)=0.
            COF(I,J-1,K,5,1,2)=0.
            COF(I,J-1,K,5,1,3)=0.
            COF(I,J-1,K,5,2,1)=0.
            COF(I,J-1,K,5,2,2)=0.
            COF(I,J-1,K,5,2,3)=0.
            COF(I,J-1,K,5,3,1)=0.
            COF(I,J-1,K,5,3,2)=0.
            COF(I,J-1,K,5,3,3)=0.
         ENDIF

      ENDIF

    1 CONTINUE
      END

C*********************************************************************
      SUBROUTINE ITRANZ (IDIM,JDIM,KDIM,LDIM,IL1,IL2,JL1V,JL2V,KL1,KL2,
     &                   KEYOUT,NBLK,TCOFZ,DEPTH,POIL,PWAT,PGAS,OIL,
     &                   DUNK,TRNDAT,COF,RESID,DGRO,TLAMB,SGAS)
C*********************************************************************

C  Black oil model z transport for diagonal tensor permeability

C  TCOFZ(I,J,K) = Z TRANSMISSABILITY CONSTANT, CU-FT CP / PSI DAY
C                 (INPUT, REAL*8)

C  DEPTH(I,J,K) = ELEMENT CENTER DEPTH, FT (INPUT, REAL*8)

C  POIL(I,J,K) = OIL PRESSURE, PSI (INPUT, REAL*8)

C  PWAT(I,J,K) = WATER PRESSURE, PSI (INPUT, REAL*8)

C  PGAS(I,J,K) = GAS PRESSURE, PSI (INPUT, REAL*8)

C  OIL(I,J,K) = OIL STOCK TANK VOLUME / UNIT PORE VOLUME (INPUT, REAL*8)

C  DUNK(I,J,K,1) = OIL DENSITY, LB/CU-FT (INPUT, REAL*8)
C  DUNK(I,J,K,2) = WATER DENSITY, LB/CU-FT (INPUT, REAL*8)
C  DUNK(I,J,K,3) = GAS DENSITY, LB/CU-FT (INPUT, REAL*8)

C  TRNDAT(I,J,K,) = MISC. PHYSICAL DATA (INPUT, REAL*4)
C                   (SEE IARYDAT.H FOR DEFINITIONS)

C  COF(I,J,K,N,L,M) = JACOBIAN COEFFICIENTS (INPUT AND OUTPUT, REAL*4)

C  RESID(I,J,K,N) = RESIDUALS (INPUT AND OUTPUT, REAL*8)

C  DGRO(I,J,K) = DISOLVED GAS-OIL RATIO (INPUT, REAL*8)

C  TLAMB(I,J,K,n) = LAMBDA OF PHASE n (INPUT, REAL*8)

C  SGAS(I,J,K) = GAS SATURATION (INPUT, REAL*4)

C*********************************************************************
      INCLUDE 'control.h'
      INCLUDE 'layout.h'

      INCLUDE 'ifluid.h'

      INTEGER JL1V(KDIM),JL2V(KDIM),      KEYOUT(IDIM,JDIM,KDIM)
      REAL*8 TCOFZ(IDIM,JDIM,KDIM),       DEPTH(IDIM,JDIM,KDIM),
     &       POIL(IDIM,JDIM,KDIM),        PWAT(IDIM,JDIM,KDIM),
     &       PGAS(IDIM,JDIM,KDIM),        DUNK(IDIM,JDIM,KDIM,3),
     &       RESID(IDIM,JDIM,KDIM,3),     DGRO(IDIM,JDIM,KDIM),
     &       TLAMB(IDIM,JDIM,KDIM,3),     OIL(IDIM,JDIM,KDIM)
      REAL*4 TRNDAT(IDIM,JDIM,KDIM,*),    SGAS(IDIM,JDIM,KDIM)
      REAL*4 COF(IDIM,JDIM,KDIM,$NUMCOF,3,3)
      REAL*8 DP,RO,FLUX,DD,G2,TC,DZ,DM,PL,TVW,TVO,TVG,TCP
      LOGICAL NOGASI,NOGASM

C  SET GRAVITY CONSTANT TO .5 g WITH UNITS PSI BBL / FT LB

      G2=.08905379276541D0*GRAV
      TVW=.178107585530824D0*DELTIM/WATVIS
      TVO=.178107585530824D0*DELTIM/OILVIS
      TVG=.178107585530824D0*DELTIM/GASVIS

C  LOOP OVER ELEMENT FACES AT K - 1/2

      KL2P=KL2+1
      DO 1 K=KL1,KL2P
      IF (K.EQ.KL1) THEN
         JL1=JL1V(K)
         JL2=JL2V(K)
      ELSE IF (K.LT.KL2P) THEN
         JL1=MIN(JL1V(K-1),JL1V(K))
         JL2=MAX(JL2V(K-1),JL2V(K))
      ELSE
         JL1=JL1V(KL2)
         JL2=JL2V(KL2)
      ENDIF
      DO 1 J=JL1,JL2
      DO 1 I=IL1,IL2

      TC=TCOFZ(I,J,K)
      IF (TC.NE.0.D0) THEN

         DD=(DEPTH(I,J,K)-DEPTH(I,J,K-1))*G2
         DDS=DD
         IF (SGAS(I,J,K).EQ.0.) THEN
            NOGASI=.TRUE.
         ELSE
            NOGASI=.FALSE.
         ENDIF
         IF (SGAS(I,J,K-1).EQ.0.) THEN
            NOGASM=.TRUE.
         ELSE
            NOGASM=.FALSE.
         ENDIF

C  OIL AND DISOLVED GAS, Z TRANSPORT AT K - 1/2

         TCP=TC*TVO
         DP=POIL(I,J,K)-POIL(I,J,K-1)-DD*(DUNK(I,J,K,1)+DUNK(I,J,K-1,1))
         DUM=TCP*DP

         IF (DP.LT.0.D0) THEN
            PL=TLAMB(I,J,K-1,1)
            ALAM=TCP*PL
            FLUX=TCP*PL*DP
            RO=DGRO(I,J,K-1)
            ROS=RO
            C11=ALAM*(DDS*TRNDAT(I,J,K,11)-1.)
            C11G=ROS*C11
            IF (NOGASI) THEN
               C12=ALAM*DDS*TRNDAT(I,J,K,8)
               C12G=ROS*C12
               C13=ALAM*DDS*TRNDAT(I,J,K,9)
               C13G=ROS*C13
            ELSE
               C12=0.
               C12G=0.
               C13=0.
               C13G=0.
            ENDIF
            C21=ALAM*(DDS*TRNDAT(I,J,K-1,11)+1.)-DUM*TRNDAT(I,J,K-1,2)
            IF (NOGASM) THEN
               C22=ALAM*DDS*TRNDAT(I,J,K-1,8)-DUM*TRNDAT(I,J,K-1,1)
               C23=ALAM*DDS*TRNDAT(I,J,K-1,9)-DUM*TRNDAT(I,J,K-1,3)
               C21G=ROS*C21
               DUM=FLUX/OIL(I,J,K-1)
               C22G=ROS*(C22+DUM)
               C23G=ROS*C23-DUM
            ELSE
               C22=-DUM*TRNDAT(I,J,K-1,1)
               C23=-DUM*TRNDAT(I,J,K-1,3)
               C21G=ROS*C21-FLUX*TRNDAT(I,J,K-1,13)
               C22G=ROS*C22
               C23G=ROS*C23
            ENDIF

         ELSE

            PL=TLAMB(I,J,K,1)
            FLUX=TCP*PL*DP
            ALAM=TCP*PL
            RO=DGRO(I,J,K)
            ROS=RO
            C11=ALAM*(DDS*TRNDAT(I,J,K,11)-1.)-DUM*TRNDAT(I,J,K,2)
            C21=ALAM*(DDS*TRNDAT(I,J,K-1,11)+1.)
            C21G=ROS*C21
            IF (NOGASM) THEN
               C22=ALAM*DDS*TRNDAT(I,J,K-1,8)
               C22G=ROS*C22
               C23=ALAM*DDS*TRNDAT(I,J,K-1,9)
               C23G=ROS*C23
            ELSE
               C22=0.
               C23=0.
               C22G=0.
               C23G=0.
            ENDIF
            IF (NOGASI) THEN
               C12=ALAM*DDS*TRNDAT(I,J,K,8)-DUM*TRNDAT(I,J,K,1)
               C13=ALAM*DDS*TRNDAT(I,J,K,9)-DUM*TRNDAT(I,J,K,3)
               C11G=ROS*C11
               DUM=FLUX/OIL(I,J,K)
               C12G=ROS*(C12+DUM)
               C13G=ROS*C13-DUM
            ELSE
               C12=-DUM*TRNDAT(I,J,K,1)
               C13=-DUM*TRNDAT(I,J,K,3)
               C11G=ROS*C11-FLUX*TRNDAT(I,J,K,13)
               C12G=ROS*C12
               C13G=ROS*C13
            ENDIF

         ENDIF

         IF (KEYOUT(I,J,K).GT.0) THEN
            COF(I,J,K,1,2,1)=COF(I,J,K,1,2,1)+C11
            COF(I,J,K,1,2,2)=COF(I,J,K,1,2,2)+C12
            COF(I,J,K,1,2,3)=COF(I,J,K,1,2,3)+C13
            COF(I,J,K,6,2,1)=C21
            COF(I,J,K,6,2,2)=C22
            COF(I,J,K,6,2,3)=C23
            RESID(I,J,K,2)=RESID(I,J,K,2)+FLUX
         ENDIF
         IF (KEYOUT(I,J,K-1).GT.0) THEN
            COF(I,J,K-1,7,2,1)=-C11
            COF(I,J,K-1,7,2,2)=-C12
            COF(I,J,K-1,7,2,3)=-C13
            COF(I,J,K-1,1,2,1)=COF(I,J,K-1,1,2,1)-C21
            COF(I,J,K-1,1,2,2)=COF(I,J,K-1,1,2,2)-C22
            COF(I,J,K-1,1,2,3)=COF(I,J,K-1,1,2,3)-C23
            RESID(I,J,K-1,2)=RESID(I,J,K-1,2)-FLUX
         ENDIF

C  FREE GAS, Z TRANSPORT AT K - 1/2

         FLUX=RO*FLUX
         IF (NOGASI.AND.NOGASM) GO TO 2

         TCP=TC*TVG
         IF (NOGASI) THEN
            DZ=DUNK(I,J,K-1,3)
         ELSE
            DZ=DUNK(I,J,K,3)
         ENDIF
         IF (NOGASM) THEN
            DM=DZ
         ELSE
            DM=DUNK(I,J,K-1,3)
         ENDIF
         DP=PGAS(I,J,K)-PGAS(I,J,K-1)-DD*(DZ+DM)
         DUM=TCP*DP
         IF (DP.LT.0.D0) THEN
            IF (NOGASM) GO TO 2
            PL=TLAMB(I,J,K-1,3)
            ALAM=TCP*PL
            IF (NOGASI) THEN
               C11G=C11G-ALAM
               DUM1=ALAM*(2.*DDS*TRNDAT(I,J,K-1,12)+1.)
               C21G=C21G+DUM1*TRNDAT(I,J,K-1,17)-DUM*TRNDAT(I,J,K-1,7)
               C22G=C22G+DUM1*TRNDAT(I,J,K-1,18)-DUM*TRNDAT(I,J,K-1,8)
               C23G=C23G+DUM1*TRNDAT(I,J,K-1,19)-DUM*TRNDAT(I,J,K-1,9)
            ELSE
               DUM1=ALAM*(DDS*TRNDAT(I,J,K,12)-1.)
               C11G=C11G+DUM1*TRNDAT(I,J,K,17)
               C12G=C12G+DUM1*TRNDAT(I,J,K,18)
               C13G=C13G+DUM1*TRNDAT(I,J,K,19)
               DUM1=ALAM*(DDS*TRNDAT(I,J,K-1,12)+1.)
               C21G=C21G+DUM1*TRNDAT(I,J,K-1,17)-DUM*TRNDAT(I,J,K-1,7)
               C22G=C22G+DUM1*TRNDAT(I,J,K-1,18)-DUM*TRNDAT(I,J,K-1,8)
               C23G=C23G+DUM1*TRNDAT(I,J,K-1,19)-DUM*TRNDAT(I,J,K-1,9)
            ENDIF
         ELSE
            IF (NOGASI) GO TO 2
            PL=TLAMB(I,J,K,3)
            ALAM=TCP*PL
            IF (NOGASM) THEN
               DUM1=ALAM*(2.*DDS*TRNDAT(I,J,K,12)-1.)
               C11G=C11G+DUM1*TRNDAT(I,J,K,17)-DUM*TRNDAT(I,J,K,7)
               C12G=C12G+DUM1*TRNDAT(I,J,K,18)-DUM*TRNDAT(I,J,K,8)
               C13G=C13G+DUM1*TRNDAT(I,J,K,19)-DUM*TRNDAT(I,J,K,9)
               C21G=C21G+ALAM
            ELSE
               DUM1=ALAM*(DDS*TRNDAT(I,J,K,12)-1.)
               C11G=C11G+DUM1*TRNDAT(I,J,K,17)-DUM*TRNDAT(I,J,K,7)
               C12G=C12G+DUM1*TRNDAT(I,J,K,18)-DUM*TRNDAT(I,J,K,8)
               C13G=C13G+DUM1*TRNDAT(I,J,K,19)-DUM*TRNDAT(I,J,K,9)
               DUM1=ALAM*(DDS*TRNDAT(I,J,K-1,12)+1.)
               C21G=C21G+DUM1*TRNDAT(I,J,K-1,17)
               C22G=C22G+DUM1*TRNDAT(I,J,K-1,18)
               C23G=C23G+DUM1*TRNDAT(I,J,K-1,19)
            ENDIF
         ENDIF
         FLUX=FLUX+TCP*PL*DP

    2    IF (KEYOUT(I,J,K).GT.0) THEN
            COF(I,J,K,1,3,1)=COF(I,J,K,1,3,1)+C11G
            COF(I,J,K,1,3,2)=COF(I,J,K,1,3,2)+C12G
            COF(I,J,K,1,3,3)=COF(I,J,K,1,3,3)+C13G
            COF(I,J,K,6,3,1)=C21G
            COF(I,J,K,6,3,2)=C22G
            COF(I,J,K,6,3,3)=C23G
            RESID(I,J,K,3)=RESID(I,J,K,3)+FLUX
         ENDIF
         IF (KEYOUT(I,J,K-1).GT.0) THEN
            COF(I,J,K-1,7,3,1)=-C11G
            COF(I,J,K-1,7,3,2)=-C12G
            COF(I,J,K-1,7,3,3)=-C13G
            COF(I,J,K-1,1,3,1)=COF(I,J,K-1,1,3,1)-C21G
            COF(I,J,K-1,1,3,2)=COF(I,J,K-1,1,3,2)-C22G
            COF(I,J,K-1,1,3,3)=COF(I,J,K-1,1,3,3)-C23G
            RESID(I,J,K-1,3)=RESID(I,J,K-1,3)-FLUX
         ENDIF

C  WATER EQUATION, Z TRANSPORT AT K - 1/2

         TCP=TC*TVW
         DP=PWAT(I,J,K)-PWAT(I,J,K-1)-DD*(DUNK(I,J,K,2)+DUNK(I,J,K-1,2))
         DUM=TCP*DP
         IF (DP.LT.0.D0) THEN
            PL=TLAMB(I,J,K-1,2)
            ALAM=TCP*PL
            DUM1=ALAM*(DDS*TRNDAT(I,J,K,10)-1.)
            C11=DUM1*TRNDAT(I,J,K,14)
            C12=DUM1*TRNDAT(I,J,K,15)
            C13=DUM1*TRNDAT(I,J,K,16)
            DUM1=ALAM*(DDS*TRNDAT(I,J,K-1,10)+1.)
            C21=DUM1*TRNDAT(I,J,K-1,14)-DUM*TRNDAT(I,J,K-1,5)
            C22=DUM1*TRNDAT(I,J,K-1,15)-DUM*TRNDAT(I,J,K-1,4)
            C23=DUM1*TRNDAT(I,J,K-1,16)-DUM*TRNDAT(I,J,K-1,6)
         ELSE
            PL=TLAMB(I,J,K,2)
            ALAM=TCP*PL
            DUM1=ALAM*(DDS*TRNDAT(I,J,K,10)-1.)
            C11=DUM1*TRNDAT(I,J,K,14)-DUM*TRNDAT(I,J,K,5)
            C12=DUM1*TRNDAT(I,J,K,15)-DUM*TRNDAT(I,J,K,4)
            C13=DUM1*TRNDAT(I,J,K,16)-DUM*TRNDAT(I,J,K,6)
            DUM1=ALAM*(DDS*TRNDAT(I,J,K-1,10)+1.)
            C21=DUM1*TRNDAT(I,J,K-1,14)
            C22=DUM1*TRNDAT(I,J,K-1,15)
            C23=DUM1*TRNDAT(I,J,K-1,16)
         ENDIF
         FLUX=TCP*PL*DP

         IF (KEYOUT(I,J,K).GT.0) THEN
            COF(I,J,K,1,1,1)=COF(I,J,K,1,1,1)+C11
            COF(I,J,K,1,1,2)=COF(I,J,K,1,1,2)+C12
            COF(I,J,K,1,1,3)=COF(I,J,K,1,1,3)+C13
            COF(I,J,K,6,1,1)=C21
            COF(I,J,K,6,1,2)=C22
            COF(I,J,K,6,1,3)=C23
            RESID(I,J,K,1)=RESID(I,J,K,1)+FLUX
         ENDIF
         IF (KEYOUT(I,J,K-1).GT.0) THEN
            COF(I,J,K-1,7,1,1)=-C11
            COF(I,J,K-1,7,1,2)=-C12
            COF(I,J,K-1,7,1,3)=-C13
            COF(I,J,K-1,1,1,1)=COF(I,J,K-1,1,1,1)-C21
            COF(I,J,K-1,1,1,2)=COF(I,J,K-1,1,1,2)-C22
            COF(I,J,K-1,1,1,3)=COF(I,J,K-1,1,1,3)-C23
            RESID(I,J,K-1,1)=RESID(I,J,K-1,1)-FLUX
         ENDIF

C  NO Z TRANSPORT AT K - 1/2

      ELSE

         IF (KEYOUT(I,J,K).EQ.0) THEN
            COF(I,J,K,6,1,1)=0.
            COF(I,J,K,6,1,2)=0.
            COF(I,J,K,6,1,3)=0.
            COF(I,J,K,6,2,1)=0.
            COF(I,J,K,6,2,2)=0.
            COF(I,J,K,6,2,3)=0.
            COF(I,J,K,6,3,1)=0.
            COF(I,J,K,6,3,2)=0.
            COF(I,J,K,6,3,3)=0.
         ENDIF
         IF (I.GT.1.AND.KEYOUT(I,J,K-1).EQ.0) THEN
            COF(I,J,K-1,7,1,1)=0.
            COF(I,J,K-1,7,1,2)=0.
            COF(I,J,K-1,7,1,3)=0.
            COF(I,J,K-1,7,2,1)=0.
            COF(I,J,K-1,7,2,2)=0.
            COF(I,J,K-1,7,2,3)=0.
            COF(I,J,K-1,7,3,1)=0.
            COF(I,J,K-1,7,3,2)=0.
            COF(I,J,K-1,7,3,3)=0.
         ENDIF

      ENDIF

    1 CONTINUE
      END
